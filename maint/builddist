#! /bin/csh
# $Id: builddist,v 1.63 1999/04/01 05:10:20 balay Exp balay $ 

# 
#  Makes distribution version of Petsc 2.0
#
echo "------- Have you done ALL of the following?? -----------------------------"
echo "(0) Excluded any directories not for public release (for example, src/eg, src/snes/interface/noise)"
echo "(1) Built wwwpages, latexpages by running make allmanpages?"
echo "(2) Set the version number in manual_tex.tex, manual.tex, and intro.tex?"
echo "(3) Built the users manual (and intro) in .ps and .html?"
echo "(4) Set the version number and release date in petsc.h?"
echo "(5) Rebuilt and checked all Fortran stubs?"
echo "(6) Made sure that all *.f Fortran examples (snes/examples/ex10.f) run correctly?"
echo "(7) Made sure Fortran include files match C ones?"

#
if ($#argv == 1) then
  set version=$1
  echo "Building ~/petsc$version.tar.gz"
else
  set version=""
endif
#
setenv PETSC_DIR /home/bsmith/petsc
cd /home/bsmith/petsc
make ACTION=clean tree
#
#/bin/rm -f ~/petsc
#
cd /home/bsmith
tar cFFXf /home/bsmith/petsc/maint/xclude /mcs/tmp/petsc.tar petsc
#
cd /mcs/tmp
tar xf /mcs/tmp/petsc.tar
#
# Before Creating the tarfile, make changes to the bmakefiles/makefiles,
# create tagfiles etc. permissions etc.
cd /mcs/tmp/petsc

# Eliminate chmods in the makefile
/bin/mv makefile makefile.bak
/bin/grep -v 'chmod' makefile.bak > makefile
/bin/rm -f makefile.bak
# Eliminate chmods in bmake/common
/bin/mv bmake/common bmake/common.bak
/bin/grep -v 'chmod' bmake/common.bak > bmake/common
/bin/rm -f  bmake/common.bak
# Eliminate '-u' from sun4/base
/bin/mv bmake/sun4/base bmake/sun4/base.bak
/bin/sed 's/-u//' bmake/sun4/base.bak > bmake/sun4/base
/bin/rm -f  bmake/sun4/base.bak

# Create EMACS-TAGS and VI-TAGS
make etags etags_examples TAGSDIR=`pwd`

# Clean up the external Packages from base.site files
foreach i (bmake/*/base.site)
  cat $i | sed "s/^MPE_INCLUDE/#MPE_INCLUDE/g" | sed "s/^MPE_LIB/#MPE_LIB/g" | \
    sed "s/^BS_INCLUDE/#BS_INCLUDE/g" | sed "s/^BS_LIB/#BS_LIB/g" | \
    sed "s/^CMEX/#CMEX/g" | sed "s/^MCC/#MCC/g" | \
    sed "s/^ADIC_INCLUDE/#ADIC_INCLUDE/g" | sed "s/^ADIC_LIB/#ADIC_LIB/g" | \
    sed "s/^ADIC_CC/#ADIC_CC/g" | \
    sed "s/^PVODE_INCLUDE/#PVODE_INCLUDE/g" | sed "s/^PVODE_LIB/#PVODE_LIB/g" | \
    sed "s/^PARMETIS_INCLUDE/#PARMETIS_INCLUDE/g" | sed "s/^PARMETIS_LIB/#PARMETIS_LIB/g" | \
    sed "s/^AMS_INCLUDE/#AMS_INCLUDE/g" | sed "s/^AMS_LIB/#AMS_LIB/g" | \
    sed "s/^PCONF/#PCONF/g" > $i.tmp
  /bin/mv -f $i.tmp $i
end

# Set the correct file permissions.
cd /mcs/tmp
chmod -R a-w petsc
chmod -R u+w petsc
chmod -R a+r petsc
find petsc -type d -name "*" -exec chmod a+x {} \;


/bin/rm -f /mcs/tmp/petsc.tar
if ($#argv == 1) then
  mv petsc petsc$version
endif


tar cf /mcs/tmp/petsc$version.tar petsc$version
/bin/rm -r -f /mcs/tmp/petsc$version
gzip -f /mcs/tmp/petsc$version.tar
/bin/cp /mcs/tmp/petsc$version.tar.gz ~/
/bin/rm -f /mcs/tmp/petsc$version.tar.gz
chmod ug+w ~/petsc$version.tar.gz
#
#  To test locally and build petsc.solid
#
# tar xf petsc$version.tar
# do chgrp -R petsc petsc$version
# do chmod -R g+w petsc$version
# do find petsc$version -type d -name '*' chmod g+s petsc$version
# Run du on petsc$version to make sure there is no garbage
# Test manpages and manual in petsc$version
# Do builds in petsc$version -- Remember that you must set
# PETSC_DIR in .cshrc to  point to petsc$version
#
# Put a .gz and .Z file in the ftp site.
# Add to petsc.html the current release and date (in Overview section)
# and to docs/tex/Releases
#
# After build Restore the local copy by undoing the following
