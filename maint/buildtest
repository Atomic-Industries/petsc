#! /bin/csh
# 
# Runs buildall in Petsc's petsc directory
#
#
setenv H /home/petsc/petsc
echo $H
cd $H/maint
/bin/rm -f build.log examples.log ftp.log
set day=`date | cut -f1 -d" "`
#
# Set up configurations for library building and testing

if ($day == Sun || $day == Wed || $day == Fri) then
    set M=(snake   doc              )
    set B=(O       g                )
    set A=(hpux    rs6000_p4        )
    set J=(fortran fortran          )
else if ($day == Mon || $day == Sat) then
    set M=(ptera      ryan      )
    set B=(O_complex  O         )
    set A=(alpha      IRIX      )
    set J=(nofortran  fortran   )
else
    set M=(ptera      )
    set B=(O          )
    set A=(alpha      )
    set J=(fortran    )
endif

#
set i=1
foreach machine ($M)
  set bopt    = $B[$i]
  set arch    = $A[$i]
  echo "*******************************************************" >> build.log
  echo "Build on $machine $bopt `date` " >> build.log
  rsh $machine -n "cd $H ; \
      make PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt all  >>& maint/build.log"
  echo "*******************************************************" >> build.log
  if ($J[$i] == fortran) then
    rsh $machine -n "cd $H; \
        make PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt fortran  >>& maint/build.log"
  endif
  rsh $machine -n "cd $H; chmod -R g+w lib" >& /dev/null
  echo "Finished Build on $machine  $arch $bopt `date`" >> build.log
  echo "******************************************************" >> examples.log
  echo "Build on $machine  $bopt `date` " >> examples.log
  rsh $machine -n "cd $H ;  \
       make PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt  testexamples >>& maint/examples.log"
  echo "******************************************************" >> examples.log
  rsh $machine -n "cd $H ;  \
       make PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt  ACTION=testexamples_2 tree >>& maint/examples.log"
  echo "******************************************************" >> examples.log
  if ($J[$i] == fortran) then
    rsh $machine -n "cd $H ;  \
         make PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt  ACTION=testexamples_3 tree >>& maint/examples.log"
  endif
  echo "Finished Build on $machine $arch $bopt `date`" >> examples.log
  @ i = ($i + 1)
  rsh $machine -n "cd $H ; \
    /bin/cp lib/lib$bopt/$arch/*.a /home/bsmith/petsc/lib/lib$bopt/$arch  >>& /dev/null ;\
    chmod g+w /home/bsmith/petsc/lib/lib$bopt/$arch ; \
    ranlib -t /home/bsmith/petsc/lib/lib$bopt/$arch/*.a  >>& /dev/null"
end
echo "Finished Build `date`" >> build.log
echo "Finished Build `date`" >> examples.log

echo "Errors from PETSc nightly build" > trashz
date >> trashz
cat build.log examples.log \
           | egrep -i 'error|warning|problem|failed|undefined' >> trashz
if ($status == 0) then
  echo "Errors generated in $H" > trash
  cat trashz >> trash
  /bin/mail balay < trash
else
  echo "No errors generated in $H -- hard to believe" > trash
  /bin/mail curfman < trash
endif 
/bin/rm -f trash trashz
chmod ug+w  build.log examples.log


