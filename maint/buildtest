#! /bin/csh
# 
# Compiles and tests PETSc in our nightly builds.
#
setenv account $1
setenv H       /home/$account/petsc
cd $H/maint
/bin/rm -f build.log examples.log
#
# Assign jobs to each account
#
if ($account == petsc) then
  set job = (1 2)
endif
if ($account == bsmith) then 
  set job = (3)
endif
if ($account == curfman) then 
  set job = (4 5)
endif
if ($account == balay) then 
  set job = (6 7)
endif
#
#  load the machine names and versions to test
#
set day=`date | cut -f1 -d" "`
if ($day == Sun) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(O         O_complex g         g_c++     O_c++     g_complex O_complex )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(1         1         1         1         1         1         3         )
  set J2=(0         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
if ($day == Mon) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(g         O         O_c++     g         O         g_c++     O         )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(0         1         1         1         1         1         3         )
  set J2=(1         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
if ($day == Tue) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(O         g         O_complex O         g         O         g         )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(0         1         1         1         1         1         3         )
  set J2=(2         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
if ($day == Wed) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(g         g_c++     O         O_complex g_complex O_c++     g_c++     )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(3         1         1         1         1         1         3         )
  set J2=(0         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
if ($day == Thu) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(O         g_complex g_c++     O_c++     g_c++     g         g_complex )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(0         1         1         1         1         1         3         )
  set J2=(0         2         2         2         2         2         4         )
  set J3=(3         3         3         3         3         3         5         )
endif
if ($day == Fri) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(g         O_c++     g_complex g_complex g         O_complex O_c++     )
  set A =(hpux      sun4      IRIX      freebsd   rs6000_p4 alpha     sun4_uni  )
  set J1=(0         1         1         1         1         1         3         )
  set J2=(2         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
if ($day == Sat) then
  set M =(snake     eagle     puck      oban      doc       ptera     merlin    )
  set B =(O         g_complex O         O_complex g_complex O         g_complex )
  set A =(hpux      sun4      IRIX  freebsd_nolog rs6000_p4 alpha     sun4_uni  )
  set J1=(1         1         1         1         1         1         3         )
  set J2=(0         2         2         2         2         2         4         )
  set J3=(0         3         3         3         3         3         5         )
endif
#
#  test for each machine
#
foreach i ($job)
  set mach    = $M[$i]
  set bopt    = $B[$i]
  set arch    = $A[$i]
  set job1    = $J1[$i]
  set job2    = $J2[$i]
  set job3    = $J3[$i]
  if ($mach == ptera) then
     set E = -e
  else
     set E = ""
  endif
  echo "****************************************************" >> build.log
  echo "Build on $mach $bopt `date` " >> build.log
  rsh $mach -n "cd $H ; \
    make $E PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt all >>& maint/build.log"
  echo "****************************************************" >> build.log
  rsh $mach -n "cd $H; \
    make $E PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt fortran >>& maint/build.log"
  rsh $mach -n "cd $H; chmod -R g+w lib" >& /dev/null
  echo "Finished Build on $mach  $arch $bopt `date`" >> build.log
  echo "****************************************************">>examples.log
  echo "Build on $mach  $bopt `date` " >> examples.log
  if ( $job1 != 0 ) then 
    rsh $mach -n "cd $H ;  \
      make $E PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt ACTION=testexamples_$job1 tree >>& maint/examples.log"
    echo "******************************************************" >> examples.log
  endif
  if ( $job2 != 0 ) then 
    rsh $mach -n "cd $H ;  \
      make $E PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt ACTION=testexamples_$job2 tree  >>& maint/examples.log"
    echo "******************************************************" >> examples.log
  endif
  if ( $job3 != 0 ) then 
    rsh $mach -n "cd $H ;  \
      make $E PETSC_ARCH=$arch PETSC_DIR=$H BOPT=$bopt ACTION=testexamples_$job3 tree  >>& maint/examples.log"
  endif
  echo "Finished Build on $mach $arch $bopt `date`" >> examples.log
  rsh $mach -n "cd $H ; \
    /bin/cp lib/lib$bopt/$arch/*.a /home/bsmith/petsc/lib/lib$bopt/$arch  >>& /dev/null ;\
  chmod g+w /home/bsmith/petsc/lib/lib$bopt/$arch ; \
  ranlib -t /home/bsmith/petsc/lib/lib$bopt/$arch/*.a  >>& /dev/null"
end
echo "Finished Build `date`" >> build.log
echo "Finished Build `date`" >> examples.log

chmod ug+w  build.log examples.log


