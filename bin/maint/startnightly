#! /bin/csh
#
# usage startnightly test1 test2 rel/test3 rel/test4 ...
#
set dir=`dirname $0`
set bdir=`dirname $dir`
set pdir=`dirname $bdir`
set day=`date | cut -f1 -d" "`

foreach TEST ($*)
  set SSH=ssh
  set SCP=scp
  set DASHN=-n
  set MERCURIAL=yes
  echo $TEST | grep ^rel/ > /dev/null
  if ("$?" == "0") then
    set TNAME=`echo ${TEST} | sed 's^rel/^^'`
    set PURL=https://bitbucket.org/petsc/petsc-3.3
    set BURL=https://bitbucket.org/petsc/buildsystem-3.3
    set RNAME=petsc33
  else
    set TNAME=${TEST}
    set PURL=https://bitbucket.org/petsc/petsc-dev
    set BURL=https://bitbucket.org/petsc/buildsystem
    set RNAME=petsc-dev
  endif
  source $dir/confignightly/${TNAME}

  echo "Starting $TEST : $CONF build at `date`"
  if ("${MERCURIAL}" == "yes") then
    $SSH $USR@$MACH $DASHN test "\( -d  $LOC \)"
    if ("$?" != "0") then
      if ("$PURL" != "" || "$BURL" != "") then
      echo "$LOC does not exist! Creating clones using $PURL and $BURL"
      $SSH $USR@$MACH $DASHN "hg clone -q $PURL $LOC; hg clone -q $BURL $LOC/config/BuildSystem"
      endif
    endif
    echo "Cleaning and updating clone at $USR@$MACH $LOC"
    $SSH $USR@$MACH $DASHN "cd $LOC ; hg -q revert -a; hg -q pull -u"
  else
    rsync -e ssh -az --delete  $pdir/ ${USR}@${MACH}:${LOC}
  endif
  echo "Starting: $SSH $USR@$MACH $DASHN $LOC/bin/maint/buildtest $CONF"
  $SSH $USR@$MACH $DASHN "cd $LOC; $LOC/bin/maint/buildtest $CONF" &
  sleep 120 # perhaps ease the nfs load
end
wait
