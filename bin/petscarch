#! /bin/csh -f
# $Id: petscarch,v 1.23 2000/12/15 15:04:13 bsmith Exp balay $ 
#
#   petscarch - Returns the machine's PETSc environmental variable, PETSC_ARCH.
#
#   You can place the command
#       "setenv PETSC_ARCH `$PETSC_DIR/bin/petscarch`"
#   in a .cshrc file if using the csh or tcsh shell.  Thus, if you have several 
#   machines of different types that share the same filesystem, then when 
#   you log into any of them, the PETSC_ARCH will always be set correctly.
# 
if (-e /bin/uname) then
    set LARCH = `/bin/uname -s`
    set MARCH = `/bin/uname -m`
    set VARCH = `/bin/uname -r`
else  if (-e /usr/bin/uname) then
    set LARCH = `/usr/bin/uname -s`
    set MARCH = `/usr/bin/uname -m`
    set VARCH = `/usr/bin/uname -r`
else
    echo "Unable to determine architecture"
    set LARCH = "unknown"
    set MARCH = "unknown"
    set VARCH = "unknown"
endif

# change B.10.01 -> 10.01
set VARCH = `echo $VARCH | sed 's/[A-Z,.]*//'`
# grab 10 from 10.01
set MajorVersion = `expr "$VARCH" : "\([0-9]*\)"`

if ("$LARCH" == "AIX") then
    set LARCH = "rs6000"
else if ("$LARCH" == "RIOS") then
    set LARCH = "rs6000"
else if ("$LARCH" == "HP-UX") then
    if ("$MajorVersion" ==  11) then
        set LARCH = "hpux64"
    else
        set LARCH = "hpux"
    endif
else if ("$LARCH" == "FreeBSD") then
    set LARCH = "freebsd"
else if ("$MARCH" == "CRAY Y-MP" ) then  # Crays are funny - using MARCH
    set LARCH = "t3d"
else if ("$MARCH" == "CRAY T3E" ) then
    set LARCH = "t3e"
else if ("$LARCH" == "i86pc" ) then
    set LARCH = "solaris_x86"
else if ("$LARCH" == "iris4d" || "$LARCH" == "IRIX") then
    if ("$MajorVersion" ==  5) then
        set LARCH = "IRIX5"
    else
        set LARCH = "IRIX"
    endif
else if ("$LARCH" == "SunOS") then
    if ("$MajorVersion" ==  4) then
        set LARCH = "sun4"
    else
        set LARCH = "solaris"
    endif
else if ("$LARCH" == "Linux") then
    if ("$MARCH" == "alpha") then
        set LARCH = "linux_alpha"
    else if ("$MARCH" == "ppc") then
        set LARCH = "linux_ppc"
    else
        set LARCH = "linux"
    endif
else if ("$LARCH" == "OSF1") then
    set LARCH = "alpha"
else if ("$LARCH" == "Hurd") then
        set LARCH = "hurd"
else if ("$LARCH" == "Rhapsody") then
        set LARCH = "macx"
else if ("$LARCH" == "5000") then
        set LARCH = "UXPV"
endif

echo $LARCH
exit 0
