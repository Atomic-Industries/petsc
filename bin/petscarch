#! /bin/csh -f
# $Id: petscarch,v 1.14 2000/07/17 19:48:05 balay Exp balay $ 

#
#   petscarch - Returns the machine's PETSc environmental variable, PETSC_ARCH.
#
#   You can place the command
#       "setenv PETSC_ARCH `$PETSC_DIR/bin/petscarch`"
#   in a .cshrc file if using the csh or tcsh shell.  Thus, if you have several 
#   machines of different types that share the same filesystem, then when 
#   you log into any of them, the PETSC_ARCH will always be set correctly.
# 
if (-e /bin/uname) then
    set LARCH = `/bin/uname -s`
    set MARCH = `/bin/uname -m`
    set VARCH = `/bin/uname -r`
else  if (-e /usr/bin/uname) then
    set LARCH = `/usr/bin/uname -s`
    set MARCH = `/usr/bin/uname -m`
    set VARCH = `/usr/bin/uname -r`
else
    echo "Unable to determine architecture"
    set LARCH = "unknown"
    set MARCH = "unknown"
    set VARCH = "unknown"
endif

set MajorVersion = `expr "$VARCH" : "\(.\)"`

if ("$LARCH" == "AIX") then
    set LARCH = "rs6000"
else if ("$LARCH" == "RIOS") then
    set LARCH = "rs6000"
else if ("$LARCH" == "HP-UX") then
    set LARCH = "hpux"
else if ("$LARCH" == "FreeBSD") then
    set LARCH = "freebsd"
else if ("$LARCH" == "CRAY Y-MP" ) then 
    set LARCH = "t3d"
else if ("$LARCH" == "CRAY T3E" ) then
    set LARCH = "t3e"
else if ("$LARCH" == "i86pc" ) then
    set LARCH = "solaris_x86"
else if ("$LARCH" == "iris4d" || "$LARCH" == "IRIX") then
    if ("$MajorVersion" ==  5) then
        set LARCH = "IRIX5"
    else
        set LARCH = "IRIX"
    endif
else if ("$LARCH" == "SunOS") then
    if ("$MajorVersion" ==  4) then
        set LARCH = "sun4"
    else
        set LARCH = "solaris"
    endif
else if ("$LARCH" == "Linux") then
    if ("$MARCH" == "alpha") then
        set LARCH = "linux_alpha"
    else
        set LARCH = "linux"
    endif
endif

echo $LARCH
exit 0
